apply plugin: 'com.android.library'
apply plugin: 'de.mannodermaus.android-junit5'
apply plugin: 'eu.davidea.grabver'

versioning {
    major 1
    minor 0
    preRelease "b2"
}
// For maven repository
version = versioning.name

ext {
    // Library Settings
    libraryName = "FlexibleAdapter LiveData extension"
    libraryDescription = "LiveData extension for FlexibleAdapter project"
    libraryLabels = ['android', 'flexibleadapter', 'adapter', 'livedata', 'lifecycle', 'viewmodel', 'androidarch']

    publishedGroupId = "eu.davidea"
    bintrayName = "flexible-adapter-livedata"
    artifact = bintrayName
}

android {
    compileSdkVersion targetSdk
    buildToolsVersion buildTools

    defaultConfig {
        minSdkVersion minSdk
        targetSdkVersion targetSdk
        versionCode versioning.code
        versionName versioning.name
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }
    testOptions {
        /* Android-junit5 - https://github.com/mannodermaus/android-junit5 */
        junitPlatform {
            jupiterVersion junitJupiterVer
            platformVersion junitPlatformVer
        }
    }
}

dependencies {
    // Android Arch
    implementation archLib.extensions

    // Test libraries: from AS3 we'll use JUnit 5 Jupiter, using "android-junit5" plugin
    androidTestImplementation supportLib.annotations
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test:rules:1.0.1'
    androidTestImplementation("android.arch.core:core-testing:$archExtensionsVer", {
    })
    testImplementation junit5.unitTests()
    testCompileOnly junit5.unitTestsRuntime()
    testImplementation "de.mannodermaus.gradle.plugins:android-junit5-embedded-runtime:1.0.31"
    testImplementation supportLib.recyclerView
    testImplementation project(":flexible-adapter")
}

if (project.hasProperty('all') || project.hasProperty('livedata')) {
    apply from: '../maven-install.gradle'
    apply from: '../jfrog-bintray-publish.gradle'
}